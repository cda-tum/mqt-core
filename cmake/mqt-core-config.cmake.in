# Copyright (c) 2023 - 2025 Chair for Design Automation, TUM
# Copyright (c) 2025 Munich Quantum Software Company GmbH
# All rights reserved.
#
# SPDX-License-Identifier: MIT
#
# Licensed under the MIT License

# A CMake config file for the library, to be used by external projects

@PACKAGE_INIT@

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

include(CMakeFindDependencyMacro)
find_dependency(nlohmann_json)
option(MQT_CORE_WITH_GMP "Library is configured to use GMP" @MQT_CORE_WITH_GMP@)
if(MQT_CORE_WITH_GMP)
  find_dependency(GMP)
endif()

option(MQT_CORE_ZX_SYSTEM_BOOST
       "Library is configured to use system Boost instead of the bundled Boost::multiprecision"
       @MQT_CORE_ZX_SYSTEM_BOOST@)
if(MQT_CORE_ZX_SYSTEM_BOOST)
  find_dependency(Boost @BOOST_MIN_VERSION@)
endif()

if(TARGET MQT::Core)
  return()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/Cache.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/PackageAddTest.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/PreventInSourceBuilds.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/StandardProjectSettings.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/mqt-core-targets.cmake")

if(NOT mqt-core_FIND_QUIETLY)
  message(STATUS "Found mqt-core version ${mqt-core_VERSION}")
endif()
